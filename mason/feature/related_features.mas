<%args>
    $feature
</%args>
<%once>
    use SGN::View::Feature qw/related_stats/;
</%once>
<%init>
    # make a hash of relationship_type => {
    #    upstream   => \@list_of_features,
    #    downstream => \@list_of_features,
    # }
    my %rels;
    push @{$rels{$_->[0]}{$_->[1]}}, $_->[2] for (
         ( map [ cvterm_link( $_->type ), 'downstream', $_->subject ], $feature->feature_relationship_objects  ),
         ( map [ cvterm_link( $_->type ), 'upstream',   $_->object  ], $feature->feature_relationship_subjects ),
        );

    my $ftype = $feature->type->name;

</%init>

%  for my $reltype ( sort keys %rels ) {
%    my $updown = $rels{$reltype};
%       for my $direction ( 'upstream', 'downstream' ) {
%         if( my $feats = $updown->{$direction} ) {
            <& .relatedfeats, reltype => $reltype, direction => $direction, feats => $feats, ftype => $ftype &>            
%         }
%       }
%  }

<%def .relatedfeats>
<%args>
 $reltype
 $direction
 $feats
 $ftype
</%args>
% my ($is,$are) = $reltype =~ /part[ _]of/i ? (' is',' are') : ('','');
<&| /page/info_section.mas,
   title => $direction eq 'upstream' ? qq|features this $ftype$is $reltype| : qq|features that$are $reltype this $ftype|,
   is_subsection => 1,
&>
%   if( @$feats > 4 ) {
     <& /page/columnar_table.mas,
         __tableattrs => 'summary="Feature summary" cellspacing="0" style="margin-bottom: 0.5em"',
         data         => related_stats($feats),
         __caption    => 'Summary',
         __border     => 1,
      &>
%   }
    <& /feature/feature_list.mas,
       features => $feats,
     &>
</&>

</%def>
