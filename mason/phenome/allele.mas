<%doc>
=head1 NAME

/phenome/allele.mas - a component for printing the alleles section

=head1 DESCRIPTION


Parameters:

=over 3

=item alleles

arrayref of CXGN::Phenome::Allele objects

=item user_id

logged in sp_person_id

=item curator

boolean. Is the logged user a curator?

=back

=head1 AUTHOR

Naama Menda <nm249@cornell.edu>

=cut
</%doc>

<%args>
  $alleles
  $user_id
  $curator
</%args>

<%perl>

use CXGN::Page::FormattingHelpers qw / columnar_table_html /;

my @allele_data;
my $data;

foreach my $a (@$alleles) {

  my $allele_id = $a->{allele_id};
  my $allele_synonyms;
  my @allele_aliases = $a->get_allele_aliases();
  foreach my $a_synonym (@allele_aliases) {
    $allele_synonyms .= $a_synonym->get_allele_alias() . "  ";
  }
  if ( !$allele_synonyms ) { $allele_synonyms = "[add new]"; }
  my $allele_synonym_link =
    qq |<a href= "/phenome/allele_synonym.pl?allele_id=$allele_id&amp;action=new">$allele_synonyms</a> |;

  my $a_person_id = $a->get_sp_person_id;
  my $allele_edit_link =
    ( $a_person_id && $user_id && ( $a_person_id == $user_id ) || ( $curator ) )
    ? "<a href=\"/phenome/allele.pl?action=edit&amp;allele_id=$allele_id\">[Edit]</a>"
	: '<span class="ghosted">[Edit]</span> ';

  my $phenotype   = $a->get_allele_phenotype();
  my $stock_ids   = $a->get_stock_ids();
  my $stock_count = scalar(@$stock_ids);
  my $stock_link  = qq|<a href="/phenome/allele.pl?action=view&amp;allele_id=$allele_id">$stock_count </a>|;
  push @allele_data,
    [
     (
      "<i>" . $a->get_allele_symbol . "</i>",
      $a->get_allele_name,
      $allele_synonym_link,
      qq|<div align="left"><a href="/phenome/allele.pl?action=view&amp;allele_id=$allele_id"> |
      . $phenotype
      . "</a></div>",
      $stock_link,
      $allele_edit_link,
     )
    ];
}
if (@allele_data) {
  $data = columnar_table_html(
			      headings => [
					   'Allele symbol', 'Allele name',
					   'Synonyms',      'Phenotype',
					   'Accessions',
					  ],
			      data         => \@allele_data,
			      __alt_freq   => 2,
			      __alt_width  => 1,
			      __alt_offset => 3,
			     );
}

</%perl>


<% $data %>
